plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group 'com.factset.sdk'
version '0.9.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-simple:1.7.32'
    implementation 'com.nimbusds:nimbus-jose-jwt:9.15.2'
    implementation 'org.json:json:20210307'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'com.nimbusds:oauth2-oidc-sdk:9.19'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir') as Directory
}

jacocoTestReport {
    reports {
        xml.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco.xml")

        html.required = true
        html.outputLocation = layout.buildDirectory.dir("reports/jacoco")
    }
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? System.getenv('MAVEN_SNAPSHOTS_URL') : System.getenv('MAVEN_RELEASES_URL')

            credentials {
                username = System.getenv('MAVEN_USERNAME')
                password = System.getenv('MAVEN_PASSWORD')
            }

            authentication {
                digest(BasicAuthentication)
            }
        }
    }
}
